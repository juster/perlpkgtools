#!/usr/bin/awk -f
##
# Filter a list of repo perl packages and print out of date versions.
#
# Stdin: Output from findrepopkgs.
# Output: [CPAN name] [CPAN ver] [package name] [package version]
#
# This script reads the list of all CPAN distributions from a file
# called "dist" that should be created with the "cpandists" script.

BEGIN {
    ovrd["libwww-perl"] = "libwww"
}

{
    name = $2
    sub(/^perl-/, "", $2)
    sub(/-[0-9]+$/, "", $3)
    sub(/^[0-9]+:/, "", $3)
    repo[$2] = $1
    pkg[$2] = name
    pkgver[$2] = $3
    author[$2] = $4
}

END {
    while(getline<"dists" > 0){
        k = ($1 in ovrd ? ovrd[$1] : tolower($1))
        if(!(k in pkgver) || $2 == pkgver[k]) continue
        print repo[k], pkg[k], pkgver[k], $1, $2, author[k] | "sort"
    }
}
