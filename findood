#!/usr/bin/awk -f
##
# Filter a list of repo perl packages and print out of date versions.
#
# Stdin: Output from findrepopkgs.
# Output: [CPAN name] [CPAN ver] [package name] [package version]
#
# This script reads the list of all CPAN distributions from a file
# called "dist" that should be created with the "cpandists" script.

{
    dist = $2
    sub(/^perl-/, "", dist)
    pkg[dist,1] = $1
    pkg[dist,2] = $2
    pkg[dist,3] = $4

    sub(/-[0-9]+$/, "", $3)
    sub(/^[0-9]+:/, "", $3)
    pkgver[dist] = $3
}

END {
    while (getline<"dists" > 0) {
        k = tolower($1)
        if (! (k in pkgver)) continue
        if ($2 == pkgver[k]) continue
        print pkg[k,1], pkg[k,2], pkgver[k], $1, $2, pkg[k,3] | "sort"
    }
}
