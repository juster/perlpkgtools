#!/usr/bin/awk -f
##
# Filter a list of repo perl packages and print out of date versions.
#
# Stdin: Output from findrepopkgs.
# Output: [CPAN name] [CPAN ver] [package name] [package version]
#
# This script reads the list of all CPAN distributions from a file
# called "dist" that should be created with the "cpandists" script.

$3 !~ /http:\/\/search.cpan.org\/dist\/[^/]+\/?$/ {
    print "error: funky url for " $1 ": " $3 | "cat 1>&2"
    next
}

{
    match($3, "[^/]+/?$")
    dist = substr($3, RSTART)
    sub(/\/$/, "", dist)
    pkgname[dist] = $1

    sub(/-[0-9]+$/, "", $2)
    pkgver[dist] = $2
}

END {
    while (getline<"dists" > 0) {
        if (! ($1 in pkgver)) continue
        if ($2 != pkgver[$1]) print pkgname[$1], pkgver[$1], $1, $2
    }
}
