#!/usr/bin/awk -f
##
# Filter a list of repo perl packages and print out of date versions.
#
# Stdin: Output from findrepopkgs.
# Output: [CPAN name] [CPAN ver] [package name] [package version]
#
# This script reads the list of all CPAN distributions from a file
# called "dist" that should be created with the "cpandists" script.

{
    dist = $1
    sub(/^perl-/, "", dist)
    pkgname[dist] = $1

    sub(/-[0-9]+$/, "", $2)
    sub(/^[0-9]+:/, "", $2)
    pkgver[dist] = $2
}

END {
    while (getline<"dists" > 0) {
        key = tolower($1)
        if (! (key in pkgver)) continue
        if ($2 != pkgver[key]) print pkgname[key], pkgver[key], $1, $2
    }
}
